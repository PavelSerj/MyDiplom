
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	//Получаю двоичные данные из ТЗ и помещаю адрес файла в созданную переменную
	//чтобы потом можно было сохранить файл
	ДвоичныеДанныеФайла = ТекущийОбъект.ПрисоединенныйФайл.Получить();
	АдресФайлаИзТЗ = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	//помещаю данные файла в ТЗ
	Если ЗначениеЗаполнено(АдресФайла) Тогда
		АдресФайлаДляТЗ = ПолучитьИзВременногоХранилища(АдресФайла);
		ТекущийОбъект.ПрисоединенныйФайл = Новый ХранилищеЗначения(АдресФайлаДляТЗ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);	

КонецПроцедуры
#КонецОбласти


#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура СохранитьФайлВБазу(Команда)
	//создаю асинхронную процедуру отдельно
	СохранитьФайлАсинх();
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьФайлИзБазы(Команда)
	//получаю файл из базы через асинх метод, давая изначально имя файлу
	Если Не ЗначениеЗаполнено(АдресФайлаИзТЗ) Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Файл для сохранения в базе отсутствует";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПолученияФайлов;
	ПараметрыДиалога.ВыборКаталога = Ложь;
	ПараметрыДиалога.Заголовок = "Куда сохранить файл?";
	
	ПолучитьФайлССервераАсинх(АдресФайлаИзТЗ,"Файл из базы",ПараметрыДиалога);
		
КонецПроцедуры
#КонецОбласти


#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Асинх Процедура СохранитьФайлАсинх()
	//асинх помещаю файл на сервер
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Заголовок = "Выберите файл";
	ПараметрыДиалога.МножественныйВыбор = Ложь;
	
	ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх(,,, ПараметрыДиалога, УникальныйИдентификатор);
	
	Если ОписаниеФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	//помещаю адрес файла в поле, затем записываю через процедуру ПередЗаписьюНаСервере в ТЗ
	АдресФайла = ОписаниеФайла.Адрес;
	//на всякий случай сразу и сюда помещаю адрес, чтобы можено было сразу сохранить фото
	АдресФайлаИзТЗ = ОписаниеФайла.Адрес; 
		
КонецПроцедуры
#КонецОбласти

