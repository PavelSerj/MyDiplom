
#Область ОбработчикиСобытий
Процедура ОбработкаПроведения(Отказ,Режим)
	
	//переменную передаю при формировании начислений, если она станет истина, значит отказ тоже истина (не заполнен регистр сведений условия оплаты труда на дату документа)
	СтатусОтказ = Ложь;
	
	СформироватьДвиженияОсновныеНачисления(СтатусОтказ);
	
	//если отказ истина, нет смысле делать что-то дальше
	Отказ = СтатусОтказ;
	Если Отказ = Ложь Тогда
		РасчитатьОклад();
		РасчитатьОтпуск();
	
		СформироватьДвижениеВзаиморасчетыССотрудником();
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура СформироватьДвиженияОсновныеНачисления(СтатусОтказ)

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ДатаНачала КАК ДатаНачала,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ДатаОкончания КАК ДатаОкончания,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ГрафикРаботы КАК ГрафикРаботы
		|ПОМЕСТИТЬ ВТ_Док
		|ИЗ
		|	Документ.ВКМ_НачислениеЗаработнойПлаты.ОсновныеНачисления КАК ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления
		|ГДЕ
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.Ссылка = &Ссылка
		|	И ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ВидРасчета = &ВидРасчетаОклад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВКМ_УсловияОплатыТрудаСрезПоследних.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ВКМ_УсловияОплатыТрудаСрезПоследних.Оклад, 0) КАК Оклад,
		|	ЕСТЬNULL(ВКМ_УсловияОплатыТрудаСрезПоследних.ПроцентОтРабот, 0) КАК ПроцентОтРабот,
		|	ВТ_Док.ГрафикРаботы КАК ГрафикРаботы
		|ПОМЕСТИТЬ ВТ_УсловияТруда
		|ИЗ
		|	РегистрСведений.ВКМ_УсловияОплатыТруда.СрезПоследних(&Дата, ) КАК ВКМ_УсловияОплатыТрудаСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Док КАК ВТ_Док
		|		ПО (ВТ_Док.Сотрудник = ВКМ_УсловияОплатыТрудаСрезПоследних.Сотрудник)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Док.Сотрудник КАК Сотрудник,
		|	ВТ_УсловияТруда.Оклад КАК Оклад,
		|	ВТ_УсловияТруда.ПроцентОтРабот КАК Процент,
		|	ЕСТЬNULL(ВКМ_ВыполненныеСотрудникомРаботыОбороты.ЧасовКОплатеОборот, 0) КАК ЧасовКОплате,
		|	ЕСТЬNULL(ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаКОплатеОборот, 0) КАК СуммаКОплате,
		|	ВТ_Док.ГрафикРаботы КАК ГрафикРаботы,
		|	ВТ_Док.ДатаНачала КАК ДатаНачала,
		|	ВТ_Док.ДатаОкончания КАК ДатаОкончания,
		|	ВТ_Док.ВидРасчета КАК ВидРасчета
		|ИЗ
		|	ВТ_УсловияТруда КАК ВТ_УсловияТруда,
		|	ВТ_Док КАК ВТ_Док
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&ДатаНачала, &ДатаОкончания, , ) КАК ВКМ_ВыполненныеСотрудникомРаботыОбороты
		|		ПО ВТ_Док.Сотрудник = ВКМ_ВыполненныеСотрудникомРаботыОбороты.Сотрудник
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.Сотрудник,
		|	NULL,
		|	NULL,
		|	NULL,
		|	NULL,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ГрафикРаботы,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ДатаНачала,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ДатаОкончания,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ВидРасчета
		|ИЗ
		|	Документ.ВКМ_НачислениеЗаработнойПлаты.ОсновныеНачисления КАК ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления
		|ГДЕ
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.Ссылка = &Ссылка
		|	И ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ВидРасчета = &ВидРасчетаОтпуск";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);	
	Запрос.УстановитьПараметр("Дата", Дата);  
	Запрос.УстановитьПараметр("ВидРасчетаОклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("ВидРасчетаОтпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Дата)); 
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Количество() > 0 Тогда //значит все заполнено
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
			Движение.ПериодРегистрации = Дата;                     
			Движение.ВидРасчета = ВыборкаДетальныеЗаписи.ВидРасчета;
			Движение.Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;
			Движение.ГрафикРаботы = ВыборкаДетальныеЗаписи.ГрафикРаботы;
			Движение.Показатель = ВыборкаДетальныеЗаписи.Оклад;
			Движение.Процент = ВыборкаДетальныеЗаписи.Процент; 
			Движение.ПериодДействияНачало = ВыборкаДетальныеЗаписи.ДатаНачала;
			Движение.ПериодДействияКонец = ВыборкаДетальныеЗаписи.ДатаОкончания;
			Движение.БазовыйПериодНачало = ВыборкаДетальныеЗаписи.ДатаНачала;
			Движение.БазовыйПериодКонец = ВыборкаДетальныеЗаписи.ДатаОкончания;
			
			//ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи); 
		КонецЦикла; 
	Иначе
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "На дату документа не введен процент от выполненных работ или оклад";
			Сообщение.Сообщить();
			Отказ = Истина;
			СтатусОтказ = Истина;
	КонецЕсли;
	
	Движения.ВКМ_ОсновныеНачисления.Записать();
	
КонецПроцедуры

Процедура РасчитатьОклад()

		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
			|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК ФактическийПериодДействия,
			|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия КАК ПериодДействия
			|ИЗ
			|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
			|			Регистратор = &Ссылка
			|				И ВидРасчета = &Оклад) КАК ВКМ_ОсновныеНачисленияДанныеГрафика";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Движение = Движения.ВКМ_ОсновныеНачисления[ВыборкаДетальныеЗаписи.НомерСтроки - 1];
			Движение.ОтработаноЧасов = ВыборкаДетальныеЗаписи.ФактическийПериодДействия;
			
			РезультатБезНДФЛ = ВыборкаДетальныеЗаписи.ФактическийПериодДействия / ВыборкаДетальныеЗаписи.ПериодДействия * Движение.Показатель;
			Движение.Результат = РезультатБезНДФЛ - (РезультатБезНДФЛ * 13 / 100);
			
			Если Движение.Процент <> 0 Тогда  //если процент не равен нулю добавим к результату данный процент
				Движение.Результат = Движение.Результат + (Движение.Результат * Движение.Процент / 100);
			КонецЕсли;
						
		КонецЦикла;
		
		Движения.ВКМ_ОсновныеНачисления.Записать(,Истина);
		
КонецПроцедуры

Процедура РасчитатьОтпуск()
	
	Движения.ВКМ_ОтпускаСотрудников.Записывать = Истина;
		
	//получаю временную таблицу сотрудников из документа НачислениеЗП
	//далее соединяю ВТ с Регистром расчета ОсновныеНачисления и ВзаиморасчетыССотрудниками 
	//устанавливаю периоды (получаю данные за 12 мес), помещаю в ВТ
	//далее две ВТ и регистр расчета но данные уже на дату документа
	//выше данные получаю через запрос, делаю проводку, расчитываю отпуск
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ДатаНачала КАК ДатаНачала,
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ДатаОкончания КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТ_Док
		|ИЗ
		|	Документ.ВКМ_НачислениеЗаработнойПлаты.ОсновныеНачисления КАК ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления
		|ГДЕ
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.Ссылка = &Ссылка
		|	И ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ВидРасчета = &ВидРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Док.Сотрудник КАК Сотрудник,
		|	СУММА(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия) КАК ВКМ_ЗначениеФактическийПериодДействия,
		|	СУММА(ЕСТЬNULL(ВКМ_ВзаиморасчетыССотрудникамиОбороты.СуммаОборот, 0)) КАК СуммаОборот
		|ПОМЕСТИТЬ ВТ_ПериодОклад
		|ИЗ
		|	ВТ_Док КАК ВТ_Док
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
		|				БазовыйПериодНачало >= &ДатаНачала
		|					И БазовыйПериодКонец <= &ДатаОкончания) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
		|		ПО ВТ_Док.Сотрудник = ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками.Обороты(&ДатаНачала, &ДатаОкончания, , ) КАК ВКМ_ВзаиморасчетыССотрудникамиОбороты
		|		ПО ВТ_Док.Сотрудник = ВКМ_ВзаиморасчетыССотрудникамиОбороты.Сотрудник
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Док.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Док.Сотрудник КАК Сотрудник,
		|	ВТ_ПериодОклад.ВКМ_ЗначениеФактическийПериодДействия КАК ФактическийПериодДействияЗаПериод,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ВТ_ПериодОклад.СуммаОборот КАК СуммаОборот,
		|	РАЗНОСТЬДАТ(ВТ_Док.ДатаНачала, ВТ_Док.ДатаОкончания, ДЕНЬ) КАК ДнейОтпуска
		|ИЗ
		|	ВТ_Док КАК ВТ_Док
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодОклад КАК ВТ_ПериодОклад
		|		ПО ВТ_Док.Сотрудник = ВТ_ПериодОклад.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
		|		ПО ВТ_Док.Сотрудник = ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник
		|			И ВТ_Док.ВидРасчета = ВКМ_ОсновныеНачисленияДанныеГрафика.ВидРасчета";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(ДобавитьМесяц(Дата, - 12)));
	Запрос.УстановитьПараметр("ДатаОкончания", НачалоМесяца(Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		//Движение по регистру расчета ОсновныеНачисления
		Движение = Движения.ВКМ_ОсновныеНачисления[ВыборкаДетальныеЗаписи.НомерСтроки - 1];			
		Движение.Результат = (ВыборкаДетальныеЗаписи.СуммаОборот / ВыборкаДетальныеЗаписи.ФактическийПериодДействияЗаПериод) * ВыборкаДетальныеЗаписи.ДнейОтпуска;
		
		//движение по регистру накопления ОтпускаСотрудников
		Движение = Движения.ВКМ_ОтпускаСотрудников.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;
		//списываю количество дней отпуска, для получения в дальнейшем отчета
		Движение.КоличествоДней = ВыборкаДетальныеЗаписи.ДнейОтпуска;

	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(,Истина);

КонецПроцедуры

Процедура СформироватьДвижениеВзаиморасчетыССотрудником()

	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	//из запроса получаю сумму с учетом периодов, далее делаю приход		
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисления.Результат, 0) КАК Результат
		|ИЗ
		|	Документ.ВКМ_НачислениеЗаработнойПлаты.ОсновныеНачисления КАК ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|		ПО ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.Сотрудник = ВКМ_ОсновныеНачисления.Сотрудник
		|			И ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ДатаНачала = ВКМ_ОсновныеНачисления.ПериодДействияНачало
		|			И ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.ДатаОкончания = ВКМ_ОсновныеНачисления.ПериодДействияКонец
		|ГДЕ
		|	ВКМ_НачислениеЗаработнойПлатыОсновныеНачисления.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;
		Движение.Сумма = ВыборкаДетальныеЗаписи.Результат;
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти
